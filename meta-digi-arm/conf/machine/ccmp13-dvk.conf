#@TYPE: Machine
#@NAME: ccmp13-dvk
#@DESCRIPTION: Configuration for Digi ConnectCore MP13 DVK
#@NEEDED_BSPLAYERS: layers/meta-digi

include conf/machine/include/st-machine-common-stm32mp.inc
include conf/machine/include/arm/armv7a/tune-cortexa7.inc
include conf/machine/include/digi-defaults.inc

# Include the machine configuration for Digi's ConnectCore MP1.
include conf/machine/include/ccmp1.inc

# =========================================================================
# Chip architecture
# =========================================================================
DEFAULTTUNE = "cortexa7thf-neon-vfpv4"
MACHINEOVERRIDES = "arm:armv7ve:stcommon:stm32mpcommon:ccmp1:ccmp13:ccmp13-dvk"

# =========================================================================
# boot device
# =========================================================================
# Configure the list of boards that enable NAND
DEVICE_BOARD_ENABLE:NAND   += "ccmp13-dvk"

# =========================================================================
# U-Boot configs
# =========================================================================
# Last one is the default (the one the symlinks point at)
UBOOT_CONFIG = "ccmp13_dvk"
UBOOT_CONFIG[ccmp13_dvk] = "ccmp13_dvk_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"

# Platform U-Boot settings
UBOOT_DTB_NAME = "ccmp13-dvk.dtb"
UBOOT_PREFIX = "u-boot"
UBOOT_SUFFIX = "bin"
UBOOT_HAS_FASTBOOT = "false"

# Set U-Boot FIP settings
FIP_UBOOT_DTB = "u-boot"
FIP_UBOOT_CONFIG = "optee"

# =========================================================================
# Machine settings
# =========================================================================
# Define list of devicetree per board
STM32MP_DEVICETREE ??= "ccmp13-dvk"
# Extra DTB for board - need to specify it with .dtb ...
STM32MP_KERNEL_DEVICETREE:ccmp13-dvk += "ccmp133-dvk.dtb"

# =========================================================================
# Machine features
# =========================================================================
MACHINE_FEATURES += "bluetooth"
MACHINE_FEATURES += "wifi"
MACHINE_FEATURES += "mca"

# =========================================================================
# Firmware
# =========================================================================
MACHINE_FIRMWARE:append = " firmware-murata firmware-murata-mfgtest"

MACHINE_EXTRA_RRECOMMENDS += " \
    ${MACHINE_FIRMWARE} \
"

# =========================================================================
# Image
# =========================================================================
# Image FS types
IMAGE_FSTYPES += '${@bb.utils.contains("IMAGE_FEATURES", "read-only-rootfs", \
                                       "tar.bz2 boot.ubifs recovery.ubifs squashfs", \
                                       "tar.bz2 ubifs boot.ubifs recovery.ubifs", d)}'

# For populate_sdk, gcc-arm-none-eabi_9 has a python2 dependency, so we remove it.
ST_TOOLS_FOR_SDK:remove = "nativesdk-gcc-arm-none-eabi"

# =========================================================================
# flashlayout
# =========================================================================
# Define the config labels to use to generate flashlayout file

# =========================================================================
# extlinux configuration
# =========================================================================
# As example, modify the default boot config for each target to M4 config

# =========================================================================
# Debug trace
# =========================================================================
# activate/deactivate the debug and trace on boot stage
ST_DEBUG_TRACE = "0"

# =========================================================================
# trusted-firmware-a
# =========================================================================
# Configure trusted-firmware-a build
TF_A_CONFIG = "optee nand usb"

# =========================================================================
# optee
# =========================================================================
# Map OPTEE configuration to device tree list
OPTEE_CONF = "ccmp13-dvk"

# =========================================================================
# Flashlayouts
# =========================================================================

# =========================================================================
# Kernel
# =========================================================================
KERNEL_IMAGETYPE = "zImage"
KERNEL_ALT_IMAGETYPE = "Image uImage vmlinux"
KERNEL_DEFCONFIG ?= "ccmp1_defconfig"
ST_KERNEL_LOADADDR ?= "0xC0008000"

# =========================================================================
# DEY settings
# =========================================================================
IMAGE_CLASSES = "image_types_digi image_types-stubi"

# U-Boot environment offset (within partition)
UBOOT_ENV_OFFSET ?= "0x0"
# U-Boot environment size
UBOOT_ENV_SIZE ?= "0x20000"
# U-Boot environment range: size (in hex) in the environment partition that
# the U-Boot environment can take up (if undefined, it will take up all the
# available space in the environment partition)
UBOOT_ENV_RANGE ?= ""

# mkfs.ubifs parameters for boot partition (the one holding kernel and device tree files)
# Max LEB count (-c 255) calculated for a partition of up to 32 MiB considering 128 KiB erase-block size.
MKUBIFS_BOOT_ARGS ?= "-m 2048 -e 126976 -c 255"

# mkfs.ubifs parameters for rootfs partition
# Max LEB count (-c 8191) calculated for a partition of up to 1 GiB considering 128 KiB erase-block size.
MKUBIFS_ARGS ?= "-m 2048 -e 126976 -c 8191"

# Wireless external module
HAS_WIFI_VIRTWLANS = "true"

# Remove additional bluetooth packages
MACHINE_BLUETOOTH_EXTRA_INSTALL = ""

# =========================================================================
# alsa
# =========================================================================
ALSA_LIST = ""

# =========================================================================
# DISTRO features
# =========================================================================
DISTRO_FEATURES:remove = " opengl "
DISTRO_FEATURES:remove = " x11 "

# add support of optee
DISTRO_FEATURES:append = " optee "

# add support of splashscreen
#DISTRO_FEATURES:append = " splashscreen "

# add support of systemd
DISTRO_FEATURES:append = " systemd "

# add support of efi
DISTRO_FEATURES:append = " efi "

# add support of InitRD installation package
DISTRO_FEATURES:append = " initrd "

# add support to pulseaudio
DISTRO_FEATURES:append = "pulseaudio"

# Disabling sysvinit
DISTRO_FEATURES_BACKFILL_CONSIDERED += "sysvinit"

VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"

# Disable use of vendorfs partition
ST_VENDORFS = "0"
