#@TYPE: Machine
#@NAME: ConnectCore 8M Mini Development Kit.
#@DESCRIPTION: Machine configuration for Digi's ConnectCore 8M Mini DVK.

DIGI_FAMILY = "ccimx8mm"
MACHINEOVERRIDES =. "mx8:mx8m:mx8mm:ccimx8m:${DIGI_FAMILY}:"

# Include the machine configuration for Digi's ConnectCore 8M module.
include conf/machine/include/ccimx8m.inc

BOOTLOADER_SEEK_USERDATA = "33"
BOOTLOADER_SEEK_BOOT = "0"

# U-Boot configurations
#
# At the moment config label should be "machine" because SDIMG_BOOTLOADER
# expects a boot artifact named after machine to create the sdcard image.
UBOOT_CONFIG ??= "ccimx8mm-dvk"
UBOOT_CONFIG[ccimx8mm-dvk] = "ccimx8mm_dvk_defconfig"
SPL_BINARY = "spl/u-boot-spl.bin"

# Set u-boot DTB
UBOOT_DTB_NAME = "ccimx8mm-dvk.dtb"

ATF_PLATFORM = "imx8mm"
IMX_BOOT_SOC_TARGET = "iMX8MM"

KERNEL_DEVICETREE ?= " \
    digi/ccimx8mm-dvk.dtb \
    digi/_ov_board_flexspi_ccimx8m-dvk.dtbo \
    digi/_ov_board_gpio-watchdog_ccimx8m-dvk.dtbo \
    digi/_ov_board_hsd101pfw2-lvds_ccimx8m-dvk.dtbo \
    digi/_ov_board_lvds_ccimx8m-dvk.dtbo \
    digi/_ov_board_user-leds_ccimx8m-dvk.dtbo \
    digi/_ov_som_bt_ccimx8m.dtbo \
    digi/_ov_som_mca-keypad_ccimx8m.dtbo \
    digi/_ov_som_wifi_ccimx8m.dtbo \
"

SERIAL_CONSOLES ?= "115200;ttymxc0"

# Bluetooth tty
BT_TTY ?= "ttymxc1"

# XBees
XBEE_RESET_N_GPIO ?= "mca-gpio@15,gpio1@8"
XBEE_SLEEP_RQ_GPIO ?= "mca-gpio@11,gpio1@7"
XBEE_TTY ?= "ttymxc3"

# Image FS types
IMAGE_FSTYPES ?= '${@bb.utils.contains("IMAGE_FEATURES", "read-only-rootfs", \
				       "boot.vfat tar.bz2 recovery.vfat squashfs", \
				       "boot.vfat ext4.gz sdcard.gz tar.bz2 recovery.vfat", d)}'

# Boot artifacts to be copied from the deploy dir to the installer ZIP
BOOTABLE_ARTIFACTS = "imx-boot-ccimx8mm-dvk.bin"
