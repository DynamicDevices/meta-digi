#@TYPE: Machine
#@NAME: ConnectCore 8X Single Board Computer Pro.
#@DESCRIPTION: Machine configuration for Digi's ConnectCore 8X SBC Pro.

# Include the machine configuration for Digi's ConnectCore 8X module.
include conf/machine/include/ccimx8x.inc

# Wireless external module
WIRELESS_MODULE_append = " ${@oe.utils.conditional('HAVE_WIFI', '1', 'kernel-module-qualcomm', '', d)}"

# Wireless p2p interface
WLAN_P2P_INTERFACE ?= "p2p0"

# Firmware
MACHINE_FIRMWARE_append = " ${@oe.utils.conditional('HAVE_BT', '1', 'firmware-qualcomm-qca6574-bt', '', d)}"
MACHINE_FIRMWARE_append = " ${@oe.utils.conditional('HAVE_WIFI', '1', 'firmware-qualcomm-qca6574-wifi', '', d)}"

# U-Boot configurations
# Last one is the default (the one the symlinks point at)
UBOOT_CONFIG ??= "ccimx8x_sbc_pro512MB ccimx8x_sbc_pro1GB ccimx8x_sbc_pro2GB"
UBOOT_CONFIG[ccimx8x_sbc_pro2GB] = "ccimx8x_sbc_pro2GB_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
UBOOT_CONFIG[ccimx8x_sbc_pro1GB] = "ccimx8x_sbc_pro1GB_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
UBOOT_CONFIG[ccimx8x_sbc_pro512MB] = "ccimx8x_sbc_pro512MB_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"

KERNEL_DEVICETREE ?= " \
    digi/ccimx8dx-sbc-pro-wb.dtb \
    digi/ccimx8dx-sbc-pro.dtb \
    digi/ccimx8qxp-sbc-pro-id135.dtb \
    digi/ccimx8qxp-sbc-pro-wb.dtb \
    digi/ccimx8qxp-sbc-pro.dtb \
"

SERIAL_CONSOLES ?= "115200;ttyLP2"

# Bluetooth tty
BT_TTY ?= "ttyLP1"

# XBee
XBEE_RESET_N_GPIO ?= "220"
XBEE_SLEEP_RQ_GPIO ?= "216"

# U-Boot script to be copied to the boot image
BOOT_SCRIPTS = "boot.scr:boot.scr"

# Flash image types
IMAGE_FSTYPES ?= "boot.vfat ext4 sdcard tar.bz2 recovery.vfat"
